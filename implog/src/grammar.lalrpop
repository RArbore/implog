use core::str::FromStr;

use crate::ast::*;

grammar(interner: &mut NameInterner);

pub Program: Vec<StatementAST> = {
    Statement* => <>   
}

pub Statement: StatementAST = {
    Rule => StatementAST::Rule(<>),
    "?" <List<Atom>> "." => StatementAST::Question(<>),
}

Rule: RuleAST = {
    <head:Atom> ":-" <body:List<Literal>> "." => RuleAST { head, speculate: false, body },
    "[" <head:Atom> "]" ":-" <body:List<Literal>> "." => RuleAST { head, speculate: true, body },
}

Literal: LiteralAST = {
    Atom => LiteralAST { lhs: vec![], rhs: <> },
    <lhs:Atom*> "->" <rhs:Atom> => LiteralAST { lhs, rhs },
}

Atom: AtomAST = {
    <relation:Symbol> "(" <terms:List<Term>> ")" => AtomAST { relation, terms },
}

Term: TermAST = {
    Symbol => TermAST::Variable(<>),
    Num => TermAST::Constant(<>),
}

Symbol: Symbol = Iden => interner.get_or_intern(<>);
Iden: &'input str = r"[a-zA-Z_][a-zA-Z0-9_]*" => <>;
Num: u32 = r"[0-9]+" => u32::from_str(<>).unwrap();

List<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};
