use core::str::FromStr;

use crate::ast::*;
use crate::representation::Symbol;

grammar();

pub Program: Vec<StatementAST> = {
    Statement* => <>   
}

Statement: StatementAST = {
    <head:Atom> ":-" <body:List<Atom>> "." => StatementAST::Rule(head, body),
    "?" <body:List<Atom>> "." => StatementAST::Question(body),
}

Atom: AtomAST = {
    Literal => AtomAST::Literal(<>),
    "[" <lit:Literal> "]" => AtomAST::Brackets(lit),
    <lhs:Literal> "->" <rhs:Literal> => AtomAST::Arrow(lhs, rhs),
}

Literal: LiteralAST = {
    Iden => LiteralAST { relation: <>, terms: vec![] },
    <relation:Iden> "(" <terms:List<Term>> ")" => LiteralAST { relation, terms },
}

Term: TermAST = {
    Iden => TermAST::Variable(<>),
    Num => TermAST::Constant(<>),
}

Iden: String = r"[a-zA-Z_][a-zA-Z0-9_]*" => <>.to_string();
Num: Symbol = r"[0-9]+" => Symbol::from_str(<>).unwrap();

List<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};
